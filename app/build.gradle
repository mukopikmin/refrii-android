apply plugin: 'com.android.application'
apply plugin: 'deploygate'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'realm-android'
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.cookpad.android.licensetools'

deploygate {
    def hash = 'git rev-parse --short HEAD'.execute([], project.rootDir).in.text.trim()

    deployments {
        developmentRelease {
            def flavor = 'development'

            message = "${flavor} build ${hash}"
            sourceFile = file("${project.rootDir}/app/${flavor}/release/app-${flavor}-release.apk")
        }

        stagingRelease {
            def flavor = 'staging'

            message = "${flavor} build ${hash}"
            sourceFile = file("${project.rootDir}/app/${flavor}/release/app-${flavor}-release.apk")
        }
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.refrii.client"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 31
        versionName "0.13.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "default"
    productFlavors {
        production {
            applicationId "com.refrii.client"
        }
        development {
            applicationId "com.refrii.client.development"
            versionNameSuffix "-dev"
        }
        staging {
            applicationId "com.refrii.client.staging"
            versionNameSuffix "-staging"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    def dagger_version = '2.21'

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    implementation 'com.google.firebase:firebase-core:17.2.0'
    implementation 'com.google.firebase:firebase-auth:19.1.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.google.android.gms:play-services-auth:17.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.google.errorprone:error_prone_annotations:2.3.3'
    implementation 'com.google.firebase:firebase-messaging:20.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.core:core:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.5.0'
    implementation 'com.squareup.picasso:picasso:2.3.3'
    implementation 'com.anupcowkur:reservoir:3.1.0'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.jakewharton:butterknife:10.1.0'
    implementation 'de.hdodenhof:circleimageview:3.0.0'
    implementation 'io.reactivex:rxjava:1.3.8'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'io.realm:android-adapters:3.1.0'
    implementation 'org.yaml:snakeyaml:1.24'

    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0'
    testImplementation 'org.mockito:mockito-inline:2.13.0'

    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    annotationProcessor 'com.jakewharton:butterknife-compiler:10.1.0'

    kapt 'com.jakewharton:butterknife-compiler:10.1.0'
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
}

kapt {
    generateStubs = true
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android-extensions'
